# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")

rust_cxx("preload_ets_cxx_gen") {
  sources = [
    "src/common/permission_check.rs",
  ]
}

ohos_static_library("preload_permission_cxx") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "${target_gen_dir}/src",
  ]
  sources = [
    "src/cxx/permission_verification.cpp",
  ]

  sources += get_target_outputs(":preload_ets_cxx_gen")

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "ipc:ipc_single",
    "rust_cxx:cxx_cppdeps",
  ]
  deps = [ ":preload_ets_cxx_gen" ]

  subsystem_name = "request"
  part_name = "request"
}

ohos_rust_static_library("preload_permission_verify") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  deps = [
    ":preload_permission_cxx"
  ]
  rustflags = [
    "-Copt-level=z",
    "-Clto=yes",
    "-Ccodegen-units=1",
  ]

  external_deps = [
    "rust_cxx:lib",
  ]
  sources = [ "src/common/mod.rs" ]
  subsystem_name = "request"
  part_name = "request"
}

ohos_rust_shared_library("cache_download_ani") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    all_ubsan = true
    debug = false
  }

  sources = [ "src/lib.rs" ]
  rustflags = [
    "-Copt-level=z",
    "-Ccodegen-units=1",
  ]

  external_deps = [
    "netmanager_base:ani_rs",
    "runtime_core:ani",
    "hilog:libhilog",
    "hilog:hilog_rust",
  ]

  deps = [
    ":preload_permission_verify",
    "../../../native/cache_download:preload_native_rlib",
    "//third_party/rust/crates/serde/serde:lib",
  ]

  clippy_lints = "none"

  subsystem_name = "request"
  part_name = "request"
  output_extension = "so"
}

generate_static_abc("cache_download") {
  base_url = "./ets"
  files = [ "ets/@ohos.request.cacheDownload.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/cache_download.abc"
}

ohos_prebuilt_etc("cache_download_etc") {
  source = "$target_out_dir/cache_download.abc"
  module_install_dir = "framework"
  subsystem_name = "request"
  part_name = "request"
  deps = [ ":cache_download" ]
}
