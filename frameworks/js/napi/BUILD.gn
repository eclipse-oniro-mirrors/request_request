# Copyright (C) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/request/request/request_aafwk.gni")
import("//build/ohos.gni")

config("upload_public_config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
  ldflags = [ "-Wl,--exclude-libs=ALL" ]
  cflags_cc = [ "-fno-exceptions" ]
  cflags = [
    "-fdata-sections",
    "-ffunction-sections",
    "-fvisibility=hidden",
  ]
}

request_src = [
  "src/download/async_call.cpp",
  "src/download_base_notify.cpp",
  "src/download_event.cpp",
  "src/download_pause.cpp",
  "src/download_query.cpp",
  "src/download_query_mimetype.cpp",
  "src/download_remove.cpp",
  "src/download_resume.cpp",
  "src/download_task_napi.cpp",
  "src/download_task_napi_V9.cpp",
  "src/header_receive_callback.cpp",
  "src/js_util.cpp",
  "src/legacy/download_manager.cpp",
  "src/legacy/download_task.cpp",
  "src/napi_utils.cpp",
  "src/notify_callback.cpp",
  "src/progress_callback.cpp",
  "src/request_module.cpp",
  "src/upload/async_call.cpp",
  "src/upload_task_napi.cpp",
  "src/upload_task_napiV5.cpp",
  "src/upload_task_napiV9.cpp",
  "src/uv_queue.cpp",
]

request_include = [
  "${arkui_path}/ace_engine/frameworks/base/utils",
  "${ability_runtime_napi_path}/featureAbility",
  "${ability_runtime_napi_path}/inner/napi_common",
  "${request_path}/frameworks/native/download/include",
  "${request_path}/frameworks/native/upload/include",
  "//third_party/libuv/include",
]

request_deps = [
  "${request_path}/common:request_common_static",
  "${request_path}/frameworks/native/download:downloadsingle",
  "//third_party/libuv:uv",
]

request_external_deps = [
  "ability_base:zuri",
  "ability_runtime:abilitykit_native",
  "ability_runtime:data_ability_helper",
  "ability_runtime:napi_base_context",
  "c_utils:utils",
  "hiviewdfx_hilog_native:libhilog",
  "ipc:ipc_single",
  "napi:ace_napi",
]

ohos_shared_library("request") {
  include_dirs = request_include
  configs = [ "//build/config/gcc:symbol_visibility_hidden" ]
  public_configs = [ ":upload_public_config" ]
  sources = request_src

  deps = request_deps
  deps += [ "${request_path}/frameworks/native/upload:upload_lib" ]
  external_deps = request_external_deps

  relative_install_dir = "module"
  subsystem_name = "request"
  part_name = "request"
}

ohos_static_library("request_static") {
  include_dirs = request_include
  configs = [ "//build/config/gcc:symbol_visibility_hidden" ]
  public_configs = [ ":upload_public_config" ]
  sources = request_src

  deps = request_deps
  external_deps = request_external_deps

  subsystem_name = "request"
  part_name = "request"
}
