# Copyright (C) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/test.gni")

rust_cxx("netstack_rs_cxx_gen") {
  sources = [ "src/wrapper.rs" ]
}

ohos_static_library("netstack_rs_cxx") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "${target_gen_dir}/src",
  ]
  sources = [ "src/cxx/wrapper.cpp" ]

  sources += get_target_outputs(":netstack_rs_cxx_gen")

  external_deps = [ "netstack:http_client" ]
  deps = [
    ":netstack_rs_cxx_gen",
    "//third_party/rust/crates/cxx:cxx_cppdeps",
  ]

  subsystem_name = "request"
  part_name = "request"
}

ohos_rust_static_library("netstack_rs") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = [ "src/lib.rs" ]

  deps = [
    ":netstack_rs_cxx",
    "../ffrt_rs:ffrt_rs",
    "//third_party/rust/crates/cxx:lib",
  ]

  subsystem_name = "request"
  part_name = "request"
}

ohos_rust_unittest("rust_netstack_rs_ut_test") {
  module_out_path = "request/request/common"

  sources = [ "src/lib.rs" ]

  deps = [
    ":netstack_rs_cxx",
    "../ffrt_rs:ffrt_rs",
    "//third_party/rust/crates/cxx:lib",
  ]

  subsystem_name = "request"
  part_name = "request"
}

group("unittest") {
  testonly = true
  deps = []
  if (!use_libfuzzer) {
    deps = [ ":rust_netstack_rs_ut_test" ]
  }
}
